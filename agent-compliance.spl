(index="cylance_protect" sourcetype=syslog_device) OR
    (index=qualys eventtype=qualys_host_summary_event TRACKING_METHOD=AGENT) OR
    (source="8640_hosts_2021-01-19T23_08_19Z.json" host="cs-host" sourcetype="_json" index=main)
| inputcsv ad_gcu.csv append=t
| eval netbios = lower(case(index=="cylance_protect",'host',index=="main",'hostname',index=="qualys",'DNS',index=="ad",netbios))
| eval agent = case(index=="main","crowdstrike",index=="ad",NULL,1==1,'index')
| eval os = coalesce(os_version,OS,os)
| eval domain = coalesce(machine_domain,ZoneNames,dest_dns)
| stats latest(_time) as latest_checkin values(lastLogonTimestamp) as lastLogonTimestamp values(OU) as ou dc(agent) as number_of_agents values(os) as os values(domain) as domain values(agent) as agent by netbios
| eval latest_checkin = coalesce(lastLogonTimestamp,strftime(latest_checkin,"%Y-%m-%dT%H:%M:%S.%Q"))
| fields - lastLogonTimestamp
| eval os = case(like(os,"%Windows Server 2008 R2%"),"windows server 2008 r2",
    like(os,"%Windows Server 2016%"),"windows server 2016",
    like(os,"%Windows Server 2012 R2%"),"windows server 2012 r2",
    like(os,"%Windows Server 2012%"),"windows server 2012",
    like(os,"%Windows Server%2008%"),"windows server 2008",
    like(os,"%Windows 10%"),"windows 10",
    like(os,"%Mac%"),"mac",
    like(os,"%mac%"),"mac",
    like(os,"%Mojave%"),"mac",
    like(os,"%Linux%"),"linux",
    like(os,"%linux%"),"linux",
    like(os,"%CentOS%"),"linux",
    like(os,"%Windows 7%"),"windows 7",
    like(os,"%Windows 8%"),"windows 8",
    like(os,"%Windows Server%2016%"),"windows server 2016",
    like(os,"%Windows Server 2019%"),"windows server 2019",
    like(os,"%Big Sur%"),"mac",
    like(os,"%Catalina%"),"mac",
    like(os,"%High Sierra%"),"mac",
    1==1,os)
| eval domain = case(like(domain,"%gcu.edu%"),"gcu.edu",like(domain,"%gce.com%"),"gce.com",like(domain,"%lopes%"),"lopes",like(domain,"%rbis%"),"orbis",like(netbios,"%orb%"),"orbis",like(netbios,"%-gce%"),"gce.com",1==1,"gcu.edu")
| eval type = case(like(os,"%server%"),"server",os=="linux","server",1==1,"workstation")
| eval has_qualys = if(like(agent,"%qualys%"),1,0)
| eval has_crowdstrike = if(like(agent,"%crowdstrike%"),1,0)
| eval has_cylance = if(like(agent,"%cylance%"),1,0)
| eval status = case(os=="mac" AND number_of_agents==2,"complete",os!="mac" AND number_of_agents==3,"complete",1==1,"incomplete")
| outputcsv agent_compliance_ad.csv
